plugins {
    id 'com.android.library'
}
apply plugin: 'maven-publish'


android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    api project(":ipc-annotation")
}

//maven私服账户名
def nexusUserName
//maven私服密码
def nexusPassword

def SNAPSHOT_REPOSITORY_URL

Properties properties = new Properties()
try {
    properties.load(new File(rootDir, "local.properties").newDataInputStream())
    nexusUserName = properties.getProperty("NEXUS_USERNAME")
    nexusPassword = properties.getProperty("NEXUS_PASSWORD")
    SNAPSHOT_REPOSITORY_URL = properties.getProperty("SNAPSHOT_REPOSITORY_URL")
} catch (Exception e) {
    e.printStackTrace()
}

// 上传源码的 task
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

//发布前先执行 clean, 避免发布的aar中使用了旧的缓存代码
publish.dependsOn clean

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'com.ycl.ipc'
                artifactId = 'ipc-bus'
                version = '0.0.1-SNAPSHOT'
                //上传源码
                artifact sourceJar

                from components.release
            }
        }

        repositories {
            mavenLocal()
            maven {
                name = "Qisi"
                url = SNAPSHOT_REPOSITORY_URL

                credentials {
                    username nexusUserName
                    password nexusPassword
                }
            }
        }
    }
}